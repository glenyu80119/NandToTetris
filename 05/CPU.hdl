// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
	Mux16(b=outalu, a=instruction[0..15], sel=instruction[15], out=inareg);
	Not(in=instruction[15], out=Ainstruc);
	Or(a=Ainstruc, b=instruction[5], out=aregload);
	ARegister(in=inareg, load=aregload, out=outareg);
	Mux(a=instruction[15], b=instruction[4], sel= instruction[15], out=dregload);
	DRegister(in=outalu, load=dregload, out=outdreg);
	
	Mux16(a = outareg, b=inM, sel=instruction[12], out=outAorM);
	ALU(x=outdreg, y=outAorM, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=outalu, zr=aluzr, ng=alung);
	And16(a=true, b=outalu, out=outM);
	And16(a=true, b=outareg, out[0..14]=addressM);
	
	Mux(a=instruction[15], b=instruction[3], sel= instruction[15], out=writeM);
	
	Or(a=aluzr, b=alung, out=zrorng);
	Not(in=zrorng, out=pos);
	And(a=aluzr, b=instruction[1], out=zrornot);
	And(a=alung, b=instruction[2], out=ngornot);
	And(a=pos, b=instruction[0], out=posornot);
	Or(a=zrornot, b=ngornot, out=zrngornot);
	Or(a=zrngornot, b=posornot, out=jump);
	Mux(a=instruction[15], b=jump, sel= instruction[15], out=jumpornot);
	
	PC(reset=reset, in=outareg, inc=true, load=jumpornot, out[0..14]=pc);
}